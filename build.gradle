group = 'io.openweathermap.sdk'
version = findProperty('version') ?: '1.0-SNAPSHOT'

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects {
    pluginManager.withPlugin('java') {
        dependencies {
            compileOnly 'org.projectlombok:lombok:1.18.34'
            annotationProcessor 'org.projectlombok:lombok:1.18.34'
            testCompileOnly 'org.projectlombok:lombok:1.18.34'
            testAnnotationProcessor 'org.projectlombok:lombok:1.18.34'
            testImplementation 'org.junit.jupiter:junit-jupiter:5.11.3'
        }

        java {
            toolchain {
                languageVersion = JavaLanguageVersion.of(findProperty('javaVersion') ?: '21')
            }

            withJavadocJar()
            withSourcesJar()
        }

        jar {
            manifest {
                attributes(
                        'Implementation-Title': project.name,
                        'Implementation-Version': project.version
                )
            }
        }

        tasks.withType(Jar).configureEach {
            def isExample = project.path.startsWith(':examples:') || project.path.startsWith(':example:')

            if (isExample) {
                from("$rootDir/example/LICENSE") { into "META-INF"; rename { "LICENSE.txt" } }
            } else {
                from("$rootDir/LICENSE") { into "META-INF"; rename { "LICENSE.txt" } }
            }
        }

        test {
            useJUnitPlatform()
        }
    }
}

configure(subprojects - project(':util')) {
    pluginManager.withPlugin('java') {
        dependencies {
            implementation project(':util')
        }
    }
}